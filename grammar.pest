WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

reserved = @{
   (  "if" | "then" | "implies" | "iff"
    | "or" | "and" | "xor"
    | "not" | "forall" | "exists"
   ) ~ !(ASCII_ALPHA | "_" | "-")
}

literal = @{ !reserved ~ (ASCII_ALPHA | "_" ) ~ (ASCII_ALPHA | "_" | "-")* }

not = _{ "not" | "!" | "~" }
negation = { not ~ term }

or = { "or" | "||" | "|" }

and = { "and" | "&&" | "&" }

implies = { "implies" | "=>" | "->" | "==>" | "-->" }

bicond = { "iff" | "<=>" | "<->" | "<==>" | "<-->" }

xor = { "xor" }

operator = {
    or | and | implies | bicond | xor
}

univ = { "forall" }

exis = { "exists" }

parenthetical = { "(" ~ expr ~ ")" }

arg = { expr }

pred = { literal ~ "(" ~ (arg ~ ",")* ~ arg? ~ ")" }

quantified = { (univ | exis) ~ literal ~ ":" ~ arg }

term = _{
     pred | quantified | parenthetical | negation | literal
}

expr = _{
    term ~ (operator ~ term)*
}

source = _{ SOI ~ expr ~ EOI }